--
declare
/*
**
** "Duck Type" support for JSON Operations
**
** Check for TREAT AS JSON Support (18.1)
** Check for CLOB support (18.1 and Patches)
** Check for VARCHAR2(32K) support.
**
** Create a package that can be used with PL/SQL conditional compliation
*/

  C_NEWLINE CONSTANT CHAR(1) := CHR(10);

  CLOB_UNSUPPORTED EXCEPTION;
  PRAGMA EXCEPTION_INIT( CLOB_UNSUPPORTED , -40449);

  EXTENDED_STRING_UNSUPPORTED EXCEPTION;
  PRAGMA EXCEPTION_INIT( EXTENDED_STRING_UNSUPPORTED , -910);

  TREAT_AS_JSON_UNSUPPORTED EXCEPTION;
  PRAGMA EXCEPTION_INIT( TREAT_AS_JSON_UNSUPPORTED , -902);

  V_PACKAGE_DEFINITION VARCHAR2(32767);
begin
  V_PACKAGE_DEFINITION := 'CREATE OR REPLACE PACKAGE JSON_FEATURE_DETECTION AS' || C_NEWLINE;

  begin
    execute immediate 'select JSON_ARRAY(DUMMY returning CLOB) from DUAL';
    V_PACKAGE_DEFINITION := V_PACKAGE_DEFINITION
	                     ||  '  CLOB_SUPPORTED CONSTANT BOOLEAN := TRUE;';
  exception
    WHEN CLOB_UNSUPPORTED THEN
	  V_PACKAGE_DEFINITION := V_PACKAGE_DEFINITION
	                       || '  CLOB_SUPPORTED CONSTANT BOOLEAN := FALSE;';
    WHEN OTHERS THEN
	  RAISE;
  end;

  begin
    execute immediate 'select * from json_table(''[]'',''$'' columns X VARCHAR2(32767) PATH ''$'')';
    V_PACKAGE_DEFINITION := V_PACKAGE_DEFINITION
	                     || '  EXTENDED_STRING_SUPPORTED CONSTANT BOOLEAN := TRUE;';
  exception
    WHEN EXTENDED_STRING_UNSUPPORTED THEN
	  V_PACKAGE_DEFINITION := V_PACKAGE_DEFINITION
	                       || '  EXTENDED_STRING_SUPPORTED CONSTANT BOOLEAN := FALSE;';
    WHEN OTHERS THEN
	  RAISE;
  end;

  begin
    execute immediate 'select TREAT(DUMMY AS JSON) from DUAL';
    V_PACKAGE_DEFINITION := V_PACKAGE_DEFINITION
	                     || '  TREAT_AS_JSON_SUPPORTED CONSTANT BOOLEAN := TRUE;';
  exception
    WHEN TREAT_AS_JSON_UNSUPPORTED THEN
	  V_PACKAGE_DEFINITION := V_PACKAGE_DEFINITION
	                       || '  TREAT_AS_JSON_SUPPORTED CONSTANT BOOLEAN := FALSE;';
    WHEN OTHERS THEN
	  RAISE;
  end;

  V_PACKAGE_DEFINITION := V_PACKAGE_DEFINITION
                       || 'END;';

  execute immediate V_PACKAGE_DEFINITION;
end;
/
--